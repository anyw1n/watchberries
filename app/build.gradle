plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'de.mannodermaus.android-junit5'
}

android {
    compileSdk rootProject.compileSdkVersion

    defaultConfig {
        applicationId "alexeyzhizhensky.watchberries"
        minSdk rootProject.minSdkVersion
        targetSdk rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
    }
}

dependencies {
    // Frontend
    implementation "com.github.PhilJay:MPAndroidChart:v$rootProject.mpAndroidChartVersion"
    implementation "com.airbnb.android:lottie:$rootProject.lottieVersion"
    implementation "io.coil-kt:coil:$rootProject.coilVersion"
    implementation "androidx.fragment:fragment-ktx:$rootProject.fragmentVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.swipeRefreshLayoutVersion"
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"

    // Backend
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.paging:paging-runtime-ktx:$rootProject.pagingVersion"

    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"
    implementation "androidx.room:room-paging:$rootProject.roomVersion"

    kapt "com.google.dagger:hilt-compiler:$rootProject.hiltVersion"
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"

    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"

    implementation platform("com.google.firebase:firebase-bom:$rootProject.firebaseBomVersion")
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-messaging-ktx"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$rootProject.desugarJdkLibsVersion"
    implementation "androidx.core:core-ktx:$rootProject.coreVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"

    // Test
    testImplementation "org.junit.jupiter:junit-jupiter:$rootProject.jUnitVersion"

    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$rootProject.spekVersion"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$rootProject.spekVersion"

    testImplementation "io.mockk:mockk:$rootProject.mockkVersion"

    testImplementation "io.strikt:strikt-core:$rootProject.striktVersion"

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$rootProject.kotlinVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.coroutinesTestVersion"
}
